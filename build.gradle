import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'groovy'
apply plugin: 'maven'

repositories{
	mavenCentral()
}

configurations {
    deployerJars
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    deployerJars "org.apache.maven.wagon:wagon-ftp:1.0-beta-2"

    compile 'org.eclipse.jetty.aggregate:jetty-all-server:8.1.10.v20130312'
}

defaultTasks 'jar'
group = "fi.jasoft.plugin"
version = project.hasProperty('BUILD_VERSION') ? getProperty('BUILD_VERSION') : 'SNAPSHOT-'+ new Date().format('yyyyMMdd')
archivesBaseName ='gradle-vaadin-plugin'
sourceCompatibility = 1.6
targetCompatibility = 1.6

uploadArchives {
    repositories.mavenDeployer {
        def repo = System.getProperty('PLUGIN_MAVEN_REPOSITORY', '<plugin repository not defined>') 
        def user = System.getProperty('PLUGIN_MAVEN_REPOSITORY_USER', '<user not defined>')
        def password = System.getProperty('PLUGIN_MAVEN_REPOSITORY_PASSWORD', '<password not defined>')

        name = 'sshDeployer' 
        configuration = configurations.deployerJars
        repository(url: repo) {
            authentication(userName: user, password: password)
        }
	    pom.project {
	            licenses {
	                license {
	                    name 'The Apache Software License, Version 2.0'
	                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	                    distribution 'repo'
	                }
	            }
	    }

	    pom.scopeMappings.mappings.remove(configurations.compile)
    }
}

// Use wrapper
task wrapper(type: Wrapper) { gradleVersion = '1.6' }

// Create pom.xml for Vaadin Directory
task createPOMforVaadinDirectory {

    File pomDir = new File("src/main/resources/META-INF/maven/${project.group}/${project.archivesBaseName}")
    pomDir.mkdirs()

    File pomFile = new File(pomDir.canonicalPath+'/pom.xml')
    outputs.file pomFile

    pom {
        artifactId = project.archivesBaseName
        project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            repositories {
                repository {
                    name 'Jasoft.fi Repository'
                    url 'http://mvn.jasoft.fi/maven2'
                }
            }
            
        }
    }.writeTo(pomFile.canonicalPath)
}

processResources {

    // Create pom.xml for Vaadin directory
    dependsOn project.createPOMforVaadinDirectory

    // Apply version
    from(sourceSets.main.resources.srcDirs){
        filter(ReplaceTokens, tokens: [version: project.version])
    }
}


jar {
    manifest{
        attributes(
            'Vaadin-Package-Version': 1,
            'Vaadin-License-Title': 'Apache 2.0',
            'Implementation-Title': 'Vaadin Plugin for Gradle',
            'Implementation-Version': version,
            'Implementation-Vendor': 'John Ahlroos',
        )
    }
}
