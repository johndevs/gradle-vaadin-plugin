/*
* Copyright 2017 John Ahlroos
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
* http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/
import org.apache.tools.ant.filters.ReplaceTokens


/***********************************************************************************************************************
 *
 * Plugins
 *
 **********************************************************************************************************************/
plugins {
    id 'groovy'
    id 'maven'
    id 'idea'
    id 'codenarc'
    id 'com.jfrog.bintray' version '1.6'
    id 'com.gradle.plugin-publish' version '0.9.1'
}

/***********************************************************************************************************************
 *
 * Basic properties
 *
 **********************************************************************************************************************/

defaultTasks 'jar'
group = "com.devsoap.plugin"
version = project.hasProperty('BUILD_VERSION') ? getProperty('BUILD_VERSION') : 'SNAPSHOT-'+ new Date().format('yyyyMMdd')
archivesBaseName ='gradle-vaadin-plugin'
sourceCompatibility = 1.8
targetCompatibility = 1.8

/***********************************************************************************************************************
 *
 * Repositories & Dependencies
 *
 **********************************************************************************************************************/
repositories{
	mavenCentral()
}

configurations {
    deploy
    proxyTest
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    // Jetty application server
    compile "org.eclipse.jetty.aggregate:jetty-all:${project.property('jetty.version')}"
    compile "org.eclipse.jetty:jetty-annotations:${project.property('jetty.version')}"
    compile "org.eclipse.jetty:jetty-plus:${project.property('jetty.version')}"
    compile "org.eclipse.jetty:jetty-deploy:${project.property('jetty.version')}"

    //Payara application server
    compile "fish.payara.extras:payara-embedded-web:${project.property('payara.version')}"

    // For GET requests
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    // Sass compilation with libSass
    compile 'io.bit3:jsass:5.3.0'

    deploy 'org.apache.maven.wagon:wagon-ssh:2.2'

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'

    // ProxyTest needs its own special configuration to avoid conflicts
    proxyTest ('org.mock-server:mockserver-netty:3.10.4') {
        exclude group:'ch.qos.logback'
    }
    project.configurations.proxyTest.exclude group: 'fish.payara.extras'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

/***********************************************************************************************************************
 *
 * Sources
 *
 **********************************************************************************************************************/
sourceSets {
    ast {
        groovy {
            srcDirs = ['src/ast']
        }
    }
}

/***********************************************************************************************************************
 *
 * Compilation
 *
 **********************************************************************************************************************/

/**
 * Compiles the AST transforms. These must be compiled before the main sources are compiled so the AST transforms
 * can be applied.
 */
task compileGroovyAstTransforms(type: GroovyCompile) {
    groovyOptions.with {
        fork = true
        keepStubs = false
        fileExtensions = ["groovy"]
    }
    source = sourceSets.ast.groovy.srcDirs
    classpath = sourceSets.main.compileClasspath
    destinationDir = sourceSets.ast.output.classesDir
}

/**
 * Compiles the sources
 */
compileGroovy {
    dependsOn compileGroovyAstTransforms
    classpath += files(sourceSets.ast.output.classesDir)
}

/**
 * Compiles the tests
 */
compileTestGroovy {
    dependsOn compileGroovyAstTransforms
    classpath += files(sourceSets.ast.output.classesDir)
    classpath += configurations.proxyTest
}

/**
 * Processes the resources before compilation
 */
processResources {

    // Directory where previously built versions are located
    def debugDir = new File(project.buildDir, 'libs').canonicalPath

    // Mark processed resources as dirty if build properties change
    inputs.property('version', project.version)
    inputs.property('debugdir', debugDir)

    // Apply build properties
    from(sourceSets.main.resources.srcDirs){
        // Files Matching is required to prevent binary files from becoming corrupt
        filesMatching('*.properties'){
            filter(ReplaceTokens, tokens: [
                    version: project.version,
                    debugdir: debugDir
            ])
        }
    }
}


/***********************************************************************************************************************
 *
 * Artifacts
 *
 **********************************************************************************************************************/

/**
 * Main plugin artifact.
 */
jar {
    manifest{
        attributes(
                'Vaadin-Package-Version': 1,
                'Vaadin-License-Title': 'Apache 2.0',
                'Implementation-Title': 'Vaadin Plugin for Gradle',
                'Implementation-Version': version,
                'Implementation-Vendor': 'John Ahlroos',
        )
    }
    from 'gradle.properties'
}

/**
 * Plugin artifact sources.
 */
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
    include 'com/**/*.groovy'
    include 'com/**/*.java'
}

/**
 * Artifacts in build
 */
artifacts {
    archives jar
    archives sourcesJar
}

/***********************************************************************************************************************
 *
 * Tests
 *
 **********************************************************************************************************************/

/**
 * Configure test phase only to run ordinary unit tests.
 */
test {
    classpath += files(sourceSets.ast.output.classesDir)
    testLogging {
        exceptionFormat "full"
    }
    exclude '**/integration/**'
}

/**
 * Run integration tests with a different tasks so they are only run when running check
 */
task integrationTest(type:Test, dependsOn:['test','jar']) {
    classpath += files(sourceSets.ast.output.classesDir)
    testLogging {
        exceptionFormat "full"
        showStandardStreams = true
    }
    include '**/integration/**'
    exclude '**/integration/*ProxyTest*'
    systemProperty 'integrationTestProjectVersion', version
    maxParallelForks = Runtime.runtime.availableProcessors() > 1 ? Runtime.runtime.availableProcessors() - 1 : 1
}
check.dependsOn 'integrationTest'

/**
 * Runs proxy integration test that needs special configuration
 */
task proxyTest(type:Test, dependsOn: ['test','jar']) {
    def oldClassPath = classpath
    classpath = configurations.proxyTest
    classpath += oldClassPath
    classpath += files(sourceSets.ast.output.classesDir)
    testLogging {
        exceptionFormat "full"
        showStandardStreams = true
    }
    include  '**/integration/*ProxyTest*'
    systemProperty 'integrationTestProjectVersion', version
}
check.dependsOn 'proxyTest'


/**
 * Code formatting configurations with Codenarc
 */
codenarc{
    toolVersion = '0.25.1'
    configFile = rootProject.file('config/codenarc/ruleset.groovy')
    maxPriority1Violations = 0
    maxPriority2Violations = 0
    maxPriority3Violations = 700
    codenarcTest {
        configFile = rootProject.file('config/codenarc/ruleset-test.groovy')
    }
}

/***********************************************************************************************************************
 *
 * Deployment
 *
 ***********************************************************************************************************************/

pluginBundle {
    website = 'https://github.com/johndevs/gradle-vaadin-plugin/wiki'
    vcsUrl = 'https://github.com/johndevs/gradle-vaadin-plugin'
    description = 'Build Vaadin applications with Gradle!'
    tags = ['vaadin']
    plugins {
        vaadinPlugin {
            id = 'com.devsoap.plugin.vaadin'
            displayName = 'Gradle Vaadin plugin'
        }
        vaadinGroovyPlugin {
            id = 'com.devsoap.plugin.vaadin.groovy'
            displayName = 'Gradle Vaadin Groovy plugin'
            description = 'Build Vaadin applications with Groovy and Gradle!'
            tags = ['vaadin', 'groovy']
        }
    }

    mavenCoordinates {
        groupId = 'com.devsoap.plugin'
        artifactId = 'gradle-vaadin-plugin'
    }
}

/***********************************************************************************************************************
 *
 * Misc.
 *
 **********************************************************************************************************************/
task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}
