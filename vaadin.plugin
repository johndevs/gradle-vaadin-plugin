/*
 * Bootstrap script for the Gradle Vaadin Plugin.
 *
 * The script will add the latest version of the plugin to the build script
 * dependencies and apply the plugin to the project. If you do not want
 * this behaviour you can copy&paste the below configuration into your
 * own build script and define your own repository and version for the plugin.
 */ 
buildscript {
    repositories {
        mavenCentral()

        def version = VersionNumber.parse(project.gradle.gradleVersion)
        def repositoryName = 'Bintray Repository'
        def repositoryUrl = 'http://dl.bintray.com/johndevs/maven'

        if (version.minor < 9 && version.major == 1){
            mavenRepo(
                name: repositoryName,
                url: repositoryUrl
            )
        }  else {
            maven({
               name = repositoryName
               url = repositoryUrl
            })
        }
    }
   dependencies {
        classpath group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'
        classpath group: 'fi.jasoft.plugin', name: 'gradle-vaadin-plugin', version: '+'
    }
}
if (!project.plugins.findPlugin(fi.jasoft.plugin.GradleVaadinPlugin)) {
    project.apply(plugin: fi.jasoft.plugin.GradleVaadinPlugin)
}

